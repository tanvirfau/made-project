pipeline gtfsProcessingPipeline {

  // Download data from the specified URL
  DataFetcher
    -> ArchiveHandler;

  // Process the downloaded zip file archive
  ArchiveHandler
    -> fileSelector
    -> textInterpreter
    -> CSVHandler
    -> TableHandler
    -> SQLiteSaver;

  block DataFetcher oftype HttpExtractor {
      url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
  }

  block ArchiveHandler oftype ArchiveInterpreter {
    archiveType: "zip";
  }

  block fileSelector oftype FilePicker {
    path: "./stops.txt";
  }

  block textInterpreter oftype TextFileInterpreter {  
    encoding: "utf8"; 
  }

  block CSVHandler oftype CSVInterpreter { 
    delimiter: ",";
    enclosing: '"';
  }

  valuetype ZoneIdentifierType oftype integer {
    constraints: [ ValidZoneConstraint ];
  } 

  block TableHandler oftype TableInterpreter {
    header: true; // CSV includes a header row
    columns: [
      "stop_id" oftype integer, 
      "stop_name" oftype text, 
      "stop_lat" oftype GeoCoordType,
      "stop_lon" oftype GeoCoordType,
      "zone_id" oftype ZoneIdentifierType
    ]; // Define column structure
  }

  constraint GeoCoordRange oftype RangeConstraint {
    lowerBound: -90;
    upperBound: 90;
    lowerBoundInclusive: true;
    upperBoundInclusive: true;
  }

  valuetype GeoCoordType oftype decimal {
    constraints: [GeoCoordRange];
  }

  constraint ValidZoneConstraint on integer: value == 1925;

  block SQLiteSaver oftype SQLiteLoader {
    table: "stops";
    file: "./gtfs.sqlite";
  }
}
