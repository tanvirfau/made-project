// Pipeline

pipeline TemperaturesPipeline {
    temperatureHttpExtractor 
    -> temperatureZipInterpreter 
    -> temperatureFilePicker 
    -> temperatureTextFileInterpreter 
    -> temperatureCSVInterpreter 
    -> columnRenameGermantoEnglish 
    -> columnRenameBatteryTemperature
    -> columnDeleter 
    -> temperatureTableInterpreter 
    -> temperaturTransformer 
    -> batteryTemperatureTransformer 
    -> temperatureDataLoader;

    block temperatureHttpExtractor oftype HttpExtractor {
        url : "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    
    block temperatureZipInterpreter oftype ArchiveInterpreter {
        archiveType : "zip";
    }

    
    block temperatureFilePicker oftype FilePicker  {
        path : "/data.csv";
    }

    block temperatureTextFileInterpreter oftype TextFileInterpreter {
    }

    block temperatureCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block columnRenameGermantoEnglish oftype CellWriter {
        at: range A1:E1;
        write : ["id", "producer", "model", "month", "temperature"];
    }

    block columnRenameBatteryTemperature oftype CellWriter {
        at: cell J1;
        write : ["battery_temperature"];
    }


    block columnDeleter oftype ColumnDeleter {
        delete : [column F, column G, column H, column I];
    }


    block temperatureTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "id" oftype positiveValueType,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype months,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal,
        ];
    }

    block temperaturTransformer oftype TableTransformer {
        inputColumns : ["temperature"];
        outputColumn : "temperature";
        uses : celsius_to_fahr;
    }

    block batteryTemperatureTransformer oftype TableTransformer {
        inputColumns : ["battery_temperature"];
        outputColumn : "battery_temperature";
        uses : celsius_to_fahr;
    }

    block temperatureDataLoader oftype SQLiteLoader {
        table: 'temperatures';
        file: 'temperatures.sqlite';
    }

    transform celsius_to_fahr {
        from celsius oftype decimal;
        to fahrenheit oftype decimal;
        fahrenheit: (celsius * 9/5) + 32;
 }
}

valuetype positiveValueType oftype integer {
    constraints : [ positiveValueConstraint ];
}

constraint positiveValueConstraint on integer :
    value >= 0;

constraint rangeMonthConst oftype RangeConstraint {
    lowerBound: 1;
    lowerBoundInclusive: true;
    upperBound: 12;
    upperBoundInclusive: true;
}

valuetype months oftype integer {
    constraints: [rangeMonthConst];
}